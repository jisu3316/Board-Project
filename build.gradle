plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    // https://github.com/node-gradle/gradle-node-plugin
    id "com.github.node-gradle.node" version "7.0.1"
    id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    asciidoctorExt
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    runtimeOnly 'com.mysql:mysql-connector-j'

    //Jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'

    // rest docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'

    // test
    testRuntimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:5.3.1'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    outputs.dir snippetsDir
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestCoverageVerification {

    violationRules {
        rule {
            // 'element'가 없으면 프로젝트의 전체 파일을 합친 값을 기준으로 한다.
            // 위의 리포트에서 'Total'로 표시된 부분이다.
            element = 'CLASS'
            limit {
                // 'counter'를 지정하지 않으면 default는 'INSTRUCTION'
                // 'value'를 지정하지 않으면 default는 'COVEREDRATIO'
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.90
            }

            // 커버리지 체크를 제외할 클래스들
            excludes = [
                    // '*.test.*',
                    '*.ProjectBoardApplication*',
                    '*.support.*',
                    '*.config.*',
                    '*.dto.*'
            ]

        }
    }
}

jacocoTestReport {
    dependsOn test
    // 리포트 작성 시 특정 파일 제외
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                            '**/ProjectBoardApplication.*',
                            '**/support/**',
                            '**/config/**',
                            '**/dto/**'
                    ])
                })
        )
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

// snippetsDir 경로를 위한 변수 설정
ext {
    snippetsDir = file('build/generated-snippets')
}

asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'

    sources { // 특정 파일만 html로 만든다.
        include '**/index.adoc'
    }
    baseDirFollowsSourceDir() // 다른 adoc 파일을 include 할 때 경로를 baseDir로 맞춘다.
    dependsOn test
}

// 배포시 문서를 포함하기 위한 설정
bootJar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

// 배포와 상관없이 로컬에서만 확인하기 위한 테스트
task copyDocument(type: Copy) {
    delete "src/main/resources/static/docs/"
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

tasks.named("jar") {
    enabled = false
}

tasks.named("bootJar") {
    archivesBaseName = "board"
    archiveFileName = "board.jar"
    archiveVersion = "0.0.1"
}

node {
    // 프론트엔드 프로젝트 경로
    nodeProjectDir = file("$projectDir/front")
    version = '21.1.0'
    download = true
}

task npmBuild(type: NpmTask) {
    dependsOn npmInstall
    args = ['run', 'build']
}

task copyFrontEnd(type: Copy) {
    dependsOn npmBuild
    from 'front/build'
    into 'build/resources/main/static/.'
}

task cleanFrontEnd(type: Delete) {
    delete "$projectDir/front/build", "$projectDir/front/node_modules"
}

npmBuild.dependsOn npmInstall
copyFrontEnd.dependsOn npmBuild
compileJava.dependsOn copyFrontEnd

clean.dependsOn cleanFrontEnd

